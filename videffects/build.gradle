apply plugin: 'com.android.library'
apply plugin: 'maven'

/**
 In order to push the library to Bintray we need to execute the
 following gradle commands in the given sequence.

 ./gradlew assemble      // To generate build folder
 ./gradlew createPom     // To create POM file for maven. if that creates a new file then dont add it to git
 ./gradlew bintrayUpload // To upload library to bintray
 */
ext {
    bintrayRepo = 'maven'
    bintrayName = 'VidEffects'

    publishedGroupId = 'com.sherazkhilji.videffects'
    libraryName = 'VidEffects'
    artifact = 'videffects'

    libraryDescription = 'It is an Android library uses OpenGL Shaders to apply effects on Videos at Runtime.'

    siteUrl = 'https://github.com/krazykira/VidEffects'
    gitUrl = 'https://github.com/krazykira/VidEffects.git'

    libraryVersion = '1.0.3'

    developerId = 'krazykira'
    developerName = 'Sheraz Ahmed Khilji'
    developerEmail = 'sherazkhilji@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}
/**
 * This is needed because we want to complie this as android library so that we can test it before publishing
 */
android {

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName libraryVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

/**
 Run this code to create pom-default.xml file in build directory
 */
task createPom doLast {
    pom {
        project {
//          All of these values are defined in the ext block
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            inceptionYear new Date()
            licenses {
                license {
                    name licenseName
                    url licenseUrl
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }.writeTo("build/poms/pom-default.xml").writeTo("pom.xml")
}
// Place it at the end of the file
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'


